class Player {
    field int x, y; // the screen location of the player's ship. Used as the top-left corner.
    field int shipGunXStart, shipGunYStart, shipGunWidth, shipGunHeight;
    field int shipTopXStart, shipTopYStart, shipTopWidth, shipTopHeight;
    field int shipMidXStart, shipMidYStart, shipMidWidth, shipMidHeight;
    field int shipBotXStart, shipBotYStart, shipBotWidth, shipBotHeight;
    field int moveDist;

    constructor Player new(int Ax, int Ay) {
        let x = Ax;
        let y = Ay;

        let shipGunXStart = 12;
        let shipGunYStart = 0;
        let shipGunWidth = 4;
        let shipGunHeight = 2;

        let shipTopXStart = 10;
        let shipTopYStart = 2;
        let shipTopWidth = 8;
        let shipTopHeight = 4;

        let shipMidXStart = 2;
        let shipMidYStart = 6;
        let shipMidWidth = 24;
        let shipMidHeight = 2;

        let shipBotXStart = 0;
        let shipBotYStart = 8;
        let shipBotWidth = 28;
        let shipBotHeight = 10;
        
        let moveDist = 2;
        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /* Draw the player ship on the screen */
    method void draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x + shipGunXStart, y + shipGunYStart, x + shipGunXStart + shipGunWidth, y + shipGunYStart + shipGunHeight);
        do Screen.drawRectangle(x + shipTopXStart, y + shipTopYStart, x + shipTopXStart + shipTopWidth, y + shipTopYStart + shipTopHeight);
        do Screen.drawRectangle(x + shipMidXStart, y + shipMidYStart, x + shipMidXStart + shipMidWidth, y + shipMidYStart + shipMidHeight);
        do Screen.drawRectangle(x + shipBotXStart, y + shipBotYStart, x + shipBotXStart + shipBotWidth, y + shipBotYStart + shipBotHeight);
        return;
    }

    method void erase() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x + shipGunXStart, y + shipGunYStart, x + shipGunXStart + shipGunWidth, y + shipGunYStart + shipGunHeight);
        do Screen.drawRectangle(x + shipTopXStart, y + shipTopYStart, x + shipTopXStart + shipTopWidth, y + shipTopYStart + shipTopHeight);
        do Screen.drawRectangle(x + shipMidXStart, y + shipMidYStart, x + shipMidXStart + shipMidWidth, y + shipMidYStart + shipMidHeight);
        do Screen.drawRectangle(x + shipBotXStart, y + shipBotYStart, x + shipBotXStart + shipBotWidth, y + shipBotYStart + shipBotHeight);
        return;
    }

    method void moveLeft() {
        if (x > 1) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x + shipGunXStart + shipGunWidth - 1, y + shipGunYStart, x + shipGunXStart + shipGunWidth, y + shipGunYStart + shipGunHeight);
            do Screen.drawRectangle(x + shipTopXStart + shipTopWidth - 1, y + shipTopYStart, x + shipTopXStart + shipTopWidth, y + shipTopYStart + shipTopHeight);
            do Screen.drawRectangle(x + shipMidXStart + shipMidWidth - 1, y + shipMidYStart, x + shipMidXStart + shipMidWidth, y + shipMidYStart + shipMidHeight);
            do Screen.drawRectangle(x + shipBotXStart + shipBotWidth - 1, y + shipBotYStart, x + shipBotXStart + shipBotWidth, y + shipBotYStart + shipBotHeight);
            let x = x - moveDist;
            do Screen.setColor(true);
            do Screen.drawRectangle(x + shipGunXStart, y + shipGunYStart, x + shipGunXStart + 1, y + shipGunYStart + shipGunHeight);
            do Screen.drawRectangle(x + shipTopXStart, y + shipTopYStart, x + shipTopXStart + 1, y + shipTopYStart + shipTopHeight);
            do Screen.drawRectangle(x + shipMidXStart, y + shipMidYStart, x + shipMidXStart + 1, y + shipMidYStart + shipMidHeight);
            do Screen.drawRectangle(x + shipBotXStart, y + shipBotYStart, x + shipBotXStart + 1, y + shipBotYStart + shipBotHeight);
        }
        return;
    }

    method void moveRight() {
        if (x < (511 - shipBotWidth - moveDist)) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x + shipGunXStart, y + shipGunYStart, x + shipGunXStart + 1, y + shipGunYStart + shipGunHeight);
            do Screen.drawRectangle(x + shipTopXStart, y + shipTopYStart, x + shipTopXStart + 1, y + shipTopYStart + shipTopHeight);
            do Screen.drawRectangle(x + shipMidXStart, y + shipMidYStart, x + shipMidXStart + 1, y + shipMidYStart + shipMidHeight);
            do Screen.drawRectangle(x + shipBotXStart, y + shipBotYStart, x + shipBotXStart + 1, y + shipBotYStart + shipBotHeight);
            let x = x + moveDist;
            do Screen.setColor(true);
            do Screen.drawRectangle(x + shipGunXStart + shipGunWidth - 1, y + shipGunYStart, x + shipGunXStart + shipGunWidth, y + shipGunYStart + shipGunHeight);
            do Screen.drawRectangle(x + shipTopXStart + shipTopWidth - 1, y + shipTopYStart, x + shipTopXStart + shipTopWidth, y + shipTopYStart + shipTopHeight);
            do Screen.drawRectangle(x + shipMidXStart + shipMidWidth - 1, y + shipMidYStart, x + shipMidXStart + shipMidWidth, y + shipMidYStart + shipMidHeight);
            do Screen.drawRectangle(x + shipBotXStart + shipBotWidth - 1, y + shipBotYStart, x + shipBotXStart + shipBotWidth, y + shipBotYStart + shipBotHeight);
        }
        return;
    }

    method int getX() {
        return x;
    }

    method int getY() {
        return y;
    }

    method int getShipGunX() {
        return shipGunXStart;
    }

    method int getShipGunY() {
        return shipGunYStart;
    }
    
    method int getShipGunWidth() {
        return shipGunWidth;
    }
}